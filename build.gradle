plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.70' apply false
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'kotlin'

    version = "0.1.0-SNAPSHOT"
    group = "io.github.wulkanowy"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.6"
            javaParameters = true
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "com.brsanthu:migbase64:2.2"
        testImplementation "junit:junit:4.13"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        testLogging.showStandardStreams = false
    }

    group = "io.github.wulkanowy.signer"
    if (project.plugins.hasPlugin('java')) {
        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }
        artifacts {
            archives sourcesJar
            archives javadocJar
        }
    }
}

dependencies {
    compile project(":hebe-jvm")
    compile project(":jvm")
}
